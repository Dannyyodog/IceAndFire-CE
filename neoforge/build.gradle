import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modImplementation "maven.modrinth:uranus:ASbYgzAy"
    modImplementation "maven.modrinth:jupiter:6FVn8sRM"

    modLocalRuntime "dev.emi:emi-neoforge:${emi_version}"

    modImplementation "maven.modrinth:jade:JkFFfEao"

    //ProjectE
    modApi "curse.maven:projecte-226410:6611984"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }

    File jsonFile = file("${destinationDir}/iceandfire.mixins.json".replace("neoforge", "common"))
    def json = jsonFile.exists() ? new JsonSlurper().parse(jsonFile) : [:]
    json.refmap = "IceAndFireCE-common-common-refmap.json"
    File f = file("${destinationDir}/iceandfire.mixins.json")
    f.parentFile.mkdirs()
    f.text = JsonOutput.prettyPrint(JsonOutput.toJson(json))
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = ""
    atAccessWideners.add("iceandfire.accesswidener")
}
